#pragma kernel CSInit
#pragma kernel CSUpdate

#define THREADS_PER_GROUP 64

#include "Assets/Shaders/HLSL/BackgroundParticles.hlsl"
#include "Assets/Shaders/HLSL/Random.hlsl"

RWStructuredBuffer<BackgroundParticleOutput> _BGParticleOutputs;
StructuredBuffer<BackgroundParticleInput> _BGParticleInputs;
uint _BackgroundMaskCount;

float3 _SpawnerMinPos;
float3 _SpawnerMaxPos;
float3 _SpawnerSize;

float _LODThreshold0, _LODThreshold1;

uint _ParticleCount;
float _TrainVelocity;

[numthreads(THREADS_PER_GROUP, 1, 1)]
void CSInit(uint id : SV_DispatchThreadID)
{
    if (id >= _ParticleCount)
    {
        return;
    }
    
    BackgroundParticleOutput output = _BGParticleOutputs[id];
    
    uint randID = HashIntToInt(id);
    uint randMod = randID % _BackgroundMaskCount;
    float rand01 = HashIntTo01Float(randID);
    
    BackgroundParticleInput input = _BGParticleInputs[randMod];
    
    output.position.x = _SpawnerMinPos.x + (_SpawnerSize.x * rand01);
    output.position.y = input.heightPos + lerp(-input.heightRange, input.heightRange, rand01);
    output.position.z = _SpawnerMinPos.z + (((float) id / (float) _ParticleCount) * _SpawnerSize.z);
    
    output.parallaxFactor = abs(output.position.z - _SpawnerMaxPos.z) / _SpawnerMaxPos.z;
    output.alive = 1;
    output.backgroundMask = input.bgMask;
    
    if (output.position.z < _LODThreshold0)
    {
        output.lodLevel = 0;

    }
    else if (output.position.z < _LODThreshold1)
    {
        output.lodLevel = 1;
    }
    else
    {
        output.lodLevel = 2;
    }
    output.randID = randID;
    
    output.rand01 = rand01;
    
    _BGParticleOutputs[id] = output;
}

[numthreads(THREADS_PER_GROUP, 1, 1)]
void CSUpdate(uint id : SV_DispatchThreadID)
{
    if (id >= _ParticleCount)
    {
        return;
    }
    
    BackgroundParticleOutput output = _BGParticleOutputs[id];
    
    if (output.alive == 1)
    {
        output.position.x -= _TrainVelocity * output.parallaxFactor;
    
        if (output.position.x < _SpawnerMinPos.x)
        {
            output.alive = 0;
        }
    }
    else
    {        
        uint randID = HashIntToInt(output.randID);
        uint randMod = randID % _BackgroundMaskCount;
        float rand01 = HashIntTo01Float(randID);
        
        BackgroundParticleInput input = _BGParticleInputs[randMod];
        
        output.position.x = _SpawnerMaxPos.x;
        output.position.y = input.heightPos + lerp(-input.heightRange, input.heightRange, rand01);
        output.position.z = _SpawnerMinPos.z + (_SpawnerSize.z * rand01);

        output.parallaxFactor = abs(output.position.z - _SpawnerMaxPos.z) / _SpawnerMaxPos.z;
        output.alive = 1;
        
        output.backgroundMask = input.bgMask;
        
        if (output.position.z < _LODThreshold0)
        {
            output.lodLevel = 0;

        }
        else if (output.position.z < _LODThreshold1)
        {
            output.lodLevel = 1;
        }
        else
        {
            output.lodLevel = 2;
        }
    }
    
    _BGParticleOutputs[id] = output;
}